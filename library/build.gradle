version = '1.0.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    archivesBaseName = "bindroid"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

uploadArchives {
    repositories.mavenDeployer {
        repository(url: 'https://api.bintray.com/maven/kuhnza/maven/bindroid') {
            bintrayUser = properties.getProperty('bintray.user')
            bintrayKey = properties.getProperty('bintray.apikey')
            authentication(userName: bintrayUser, password: bintrayKey)
        }

        pom.groupId = 'com.kuhnza'
        pom.artifactId = 'bindroid'
        pom.version = '1.0.1'
        pom.project {
            name 'Bindroid'
            description 'Simple data binding for Android'

            scm {
                connection 'https://github.com/kuhnza/bindroid.git'
                developerConnection 'https://github.com/kuhnza/bindroid.git'
                url 'https://github.com/kuhnza/bindroid'
            }

            licenses {
                license {
                    name 'MIT'
                    url 'http://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'depoll'
                    name 'David Poll'
                }
                developer {
                    id 'kuhnza'
                    name 'Dave Kuhn'
                    email 'david.s.kuhn@gmail.com'
                }
            }
        }
    }
}
